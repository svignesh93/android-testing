apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.devtools.ksp'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "com.example.android.architecture.blueprints.todoapp"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testApplicationId "com.example.android.architecture.blueprints.todoapp.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        // Setting this to false as it clears coverage output files and throws exception when
        // generating coverage reports.
        testInstrumentationRunnerArguments clearPackageData: 'false'
    }
    buildTypes {
        debug {
            testCoverageEnabled true
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        project.archivesBaseName = "TodoApp"
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    // Always show the result of every unit test when running via command line, even if it passes.
    testOptions {
        unitTests {
            includeAndroidResources true
            returnDefaultValues true
        }
        unitTests.all {
            jacoco {
                includeNoLocationClasses true
                // https://github.com/gradle/gradle/issues/5184#issuecomment-457865951
                excludes += ['jdk.internal.**']
            }
        }
        animationsDisabled true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    lintOptions {
        baseline file("lint-baseline.xml")
        warningsAsErrors true
        enable "Interoperability"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas".toString())
    arg("room.incremental", "true")
    arg("room.expandProjection", "true")
}

kapt {
    correctErrorTypes true
}

dependencies {
    // App dependencies
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentKtxVersion"
    implementation "androidx.annotation:annotation:$androidXAnnotations"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipeRefreshLayoutVersion"
    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    // Architecture Components

    // Room
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    ksp "androidx.room:room-compiler:$roomVersion"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$archLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$archLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$archLifecycleVersion"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // JVM Test Suite
    testImplementation "junit:junit:$junitVersion"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "org.robolectric:robolectric:4.7.1"
    testImplementation "org.mockito:mockito-inline:4.6.1"
    testImplementation "org.mockito:mockito-android:4.6.1"
    testImplementation "com.linkedin.dexmaker:dexmaker-mockito-inline:2.28.1"
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion") {
        // https://github.com/Kotlin/kotlinx.coroutines/tree/master/kotlinx-coroutines-debug#debug-agent-and-android
        exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
    }

    // JVM Test Suite - AndroidX Test
    testImplementation "androidx.test:core-ktx:1.4.0"
    testImplementation "androidx.test.ext:junit-ktx:1.1.3"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "androidx.room:room-testing:$roomVersion"

    // Instrumentation Test Suite
    androidTestImplementation "junit:junit:$junitVersion"
    androidTestImplementation "org.mockito:mockito-inline:4.6.1"
    androidTestImplementation "org.mockito:mockito-android:4.6.1"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito-inline:2.28.1"
    androidTestImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion") {
        // https://github.com/Kotlin/kotlinx.coroutines/tree/master/kotlinx-coroutines-debug#debug-agent-and-android
        exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
    }

    // Instrumentation Test Suite - Android Test Orchestrator
    androidTestUtil "androidx.test:orchestrator:1.4.1"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"

    // Instrumentation Test Suite - Assertions
    androidTestImplementation "androidx.test.ext:truth:1.4.0"
    androidTestImplementation "androidx.test.ext:junit-ktx:$androidXTestExtKotlinRunnerVersion"

    // Instrumentation Test Suite - AndroidX Test
    androidTestImplementation "androidx.test:core-ktx:1.4.0"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    // Instrumentation Test Suite - Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espressoVersion"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion"

    // Testing code should not be included in the main code.
    // Once https://issuetracker.google.com/128612536 is fixed this can be fixed.
    debugImplementation "androidx.fragment:fragment-testing:1.5.0"
}
